/*
 * Copyright 2015-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

subprojects {
    plugins.withType(MavenPublishPlugin) {
        publishing {
            publications {
                maven(MavenPublication) {
                    groupId project.group

                    from components.java

                    if (project.name == 'rsocket-rpc-protobuf') {
                        def pluginPrefix = "rsocket-rpc-protobuf-${osdetector.os}-${osdetector.arch}"
                        def pluginName = "${pluginPrefix}.exe"
                        def artifactStagingPath = "$buildDir/artifacts" as File
                        def pathToPlugin = "${artifactStagingPath.getPath()}/${pluginName}"
                        artifact(pathToPlugin) {
                            classifier osdetector.os + "-" + osdetector.arch
                            builtBy 'buildArtifacts'
                        }
                    } else {
                        artifact sourcesJar
                    }

                    pom.withXml {
                        asNode().':version' + {
                            resolveStrategy = DELEGATE_FIRST

                            name project.name
                            description project.description
                            url 'http://rsocket.io'

                            licenses {
                                license {
                                    name 'The Apache Software License, Version 2.0'
                                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                                }
                            }

                            developers {
                                developer {
                                    id 'robertroeser'
                                    name 'Robert Roeser'
                                    email 'robert@netifi.com'
                                }
                                developer {
                                    id 'rdegnan'
                                    name 'Ryland Degnan'
                                    email 'ryland@netifi.com'
                                }
                                developer {
                                    id 'mostroverkhov'
                                    name 'Maksym Ostroverkhov'
                                    email 'maksym@netifi.com'
                                }
                            }

                            scm {
                                connection 'scm:git:https://github.com/rsocket/rsocket-rpc-kotlin.git'
                                developerConnection 'scm:git:https://github.com/rsocket/rsocket-rpc-kotlin.git'
                                url 'https://github.com/rsocket/rsocket-rpc-kotlin'
                            }
                        }
                    }
                }
            }
        }
    }
}