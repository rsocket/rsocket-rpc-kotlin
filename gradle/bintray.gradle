/*
 * Copyright 2015-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {

    subprojects {
        plugins.withId('com.jfrog.bintray') {
            bintray {
                user = project.property('bintrayUser')
                key = project.property('bintrayKey')

                publications = ['maven']
                publish = true
                override = true

                pkg {
                    repo = 'RSocket'
                    name = 'rsocket-rpc-kotlin'
                    licenses = ['Apache-2.0']

                    issueTrackerUrl = 'https://github.com/rsocket/rsocket-rpc-kotlin/issues'
                    websiteUrl = 'https://github.com/rsocket/rsocket-rpc-kotlin'
                    vcsUrl = 'https://github.com/rsocket/rsocket-rpc-kotlin.git'

                    githubRepo = 'rsocket/rsocket-rpc-kotlin' //Optional Github repository
                    githubReleaseNotesFile = 'README.md' //Optional Github readme file

                    version {
                        name = project.version
                        released = new Date()
                        vcsTag = project.version

                        gpg {
                            sign = true
                        }
                    }
                }
            }
        }

        plugins.withId('com.jfrog.artifactory') {
            artifactory {
                publish {
                    contextUrl = 'https://oss.jfrog.org'

                    repository {
                        repoKey = version.contains('SNAPSHOT') ? 'oss-snapshot-local' : 'oss-release-local'

                        // Credentials for oss.jfrog.org are a user's Bintray credentials
                        username = project.property('bintrayUser')
                        password = project.property('bintrayKey')
                    }

                    defaults {
                        publications('maven')
                    }
                }
            }
        }
    }
}
